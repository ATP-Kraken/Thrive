// Main menu script of Thrive
GUIBaseFile = "MainMenu.layout";
GUIAnimations = ["thrive.anims"];
RequireCEGUIHooked = true;




// This keeps the GUI always on (no mouse capture is used on the main menu)
o GuiCollection "OnKeeper"{
    l params{
        Enabled = true;
        KeepsGUIOn = true;
    }
}


// Top level window for all menus
o GuiCollection "Background"{
    l params{
        Enabled = true;
        AutoTarget = "Background";
    }
}

// main menu root window
o GuiCollection "TopLevelMenu"{
    l params{
        Enabled = true;
        AutoTarget = "Background/MainMenuInteractive";
        //AutoAnimateChildren = true;
        //AutoAnimationIn = [[AutoTarget], [PopIn]];
        //AutoAnimationOut = [[AutoTarget], [SinkAway]];
    }
}

// Root of the cell stage GUI
o GuiCollection "MicrobeRoot"{
    l params{
        Enabled = false;
        AutoTarget = "MicrobeStageRoot";
    }
}

// ------------------------------------ //
// This creates the intro video player
o GuiObject "_IntroVideoStarter"{
    s{
        GuiObject@ OurObject;

        // Comment the next line to not play intro video
        [@Listener="OnInit"]
        bool StartVideoPlayback(GuiObject@ instance){

            LOG_INFO("Starting to play intro video");
            CEGUI::Window@ videoPlayer = instance.GetOwningManager().GetRootWindow().
                 CreateAndAddChild("Thrive/VideoPlayer", "MainMenuVideoPlayer");

            videoPlayer.SetSize(1.0, 0, 1.0, 0);

            auto@ castedPlayer = cast<CEGUIVideoPlayer>(videoPlayer);

            // Grab the created window as our own
            instance.ConnectElement(videoPlayer);

            if(!instance.IsCEGUIEventHooked()){

                LOG_ERROR("IntroVideoStarter failed to bind to the created video "
                    "player widget");
                return false;
            }

            // Save object for callback //
            @OurObject = instance;

            // Register end callback //
            castedPlayer.OnPlaybackEnded.Register(@OnVideoEnded);

            // Start playback
            castedPlayer.Play("Data/Videos/intro.mp4");

            return true;
        }

        [@Listener="OnClick"]
        bool VideoPlayerClicked(GuiObject@ instance){

             LOG_INFO("VideoPlayer clicked");
             return true;
        }

        void DestroyVideoPlayerWindow(){
             LOG_INFO("Destroying VideoPlayer window");
             OurObject.GetOwningManager().GetRootWindow().
                 DestroyChild("MainMenuVideoPlayer");
        }

        // Called by the videoplayer when the playback ends
        void OnVideoEnded(NamedVars@ values){

             LOG_INFO("Intro video ended");
             GetEngine().Invoke(@DestroyVideoPlayerWindow);
        }
    @%};
}



// ------------------------------------ //
// Main menu objects
// Version label
o GuiObject "Background/MainMenuInteractive/VersionLabel"{
    s{
        [@Listener="OnInit"]
        bool WriteVersion(GuiObject@ instance){

            instance.GetTargetWindow().SetText("TODO: ver (" + GetLeviathanVersion() +
                 ")");

            return true;
        }
    @%};
}

// Quit the game //
o GuiObject "Background/MainMenuInteractive/ExitGameButton"{
    s{
        [@Listener="OnClick"]
        bool QuitIt(GuiObject@ instance){
            // Close the game //
            cast<LeviathanApplication>(GetThriveGame()).MarkAsClosing();
            return true;
        }
        @%};
}


// Start game //
o GuiObject "Background/MainMenuInteractive/NewGameButton"{
    s{
        [@Listener="OnClick"]
        bool StartGame(GuiObject@ instance){
            // Disable all the menus
            instance.GetOwningManager().SetCollectionState("Background", false);
            // And show the microbe GUI
            instance.GetOwningManager().SetCollectionState("MicrobeRoot", true);
            

            // And notify the application class to start a new game
            GetThriveGame().startNewGame();
            return true;
        }

        // Uncomment the next line to automatically start a new game
        // also remember to disable the intro video to not hear it also play
        // [@Listener="OnInit"]
        bool InstantStart(GuiObject@ instance){

             StartGame(instance);
             return true;
        }
        
        @%};
}

// ------------------------------------ //


